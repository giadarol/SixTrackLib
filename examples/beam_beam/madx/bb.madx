beam, particle=proton, npart = 1e11, energy=7000, exn=2.5e-6, eyn=2.5e-6;

tune_x = 0.31;
tune_y = 0.32;
beta_s = 0.40;
one_turn: matrix, 
  rm11= cos(2*pi*tune_x),        rm12=sin(2*pi*tune_x)*beta_s,
  rm21=-sin(2*pi*tune_x)/beta_s, rm22=cos(2*pi*tune_x),
  rm33= cos(2*pi*tune_y),        rm34=sin(2*pi*tune_y)*beta_s,
  rm43=-sin(2*pi*tune_y)/beta_s, rm44=cos(2*pi*tune_y)
;
!value, one_turn->rm11, one_turn->rm12, one_turn->rm21, one_turn->rm22;

beam_beam: beambeam, charge=1, xma=0, yma=0, sigx=1.1577515656772496e-05, sigy=1.1577515656772496e-05;

ring:   line=(one_turn);
ringbb: line=(one_turn, beam_beam);

use,period=ringbb;

track, dump;
start, x= 1e-5, px=0, y=0*1e-4, py=0;
start, x= 2e-5, px=0, y=0*2e-4, py=0;
start, x= 3e-5, px=0, y=0*3e-4, py=0;
start, x= 4e-5, px=0, y=0*4e-4, py=0;
start, x= 5e-5, px=0, y=0*5e-4, py=0;
run,turns=1000;
endtrack;

plot, file="MAD_track",table=track,haxis=x,vaxis=px,
particle=1,2,3,4,5, colour=100, multiple;
plot, file="MAD_track",table=track,haxis=y,vaxis=py,
particle=1,2,3,4,5, colour=100, multiple;

system, "ps2pdf MAD_track.ps && rm MAD_track.ps";

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

MAKEFOOTPRINT(): macro = {
  small=0.05;
  big=sqrt(1.-small^2);
  track;
  xs=small; ys=small;
  value,xs,ys;
  start,fx=xs,fy=ys;  // zero amplitude
  n=1; // sigma multiplier
  m=0; // angle multiplier
  while (n <= 6)
  {
    angle = 15*m*pi/180;
    if (m == 0) {xs=n*big; ys=n*small;}
    elseif (m == 6) {xs=n*small; ys=n*big;}
    else
    {
      xs=n*cos(angle);
      ys=n*sin(angle);
    }
    value,xs,ys;
    start,fx=xs,fy=ys;
    m=m+1;
    if (m == 7) { m=0; n=n+1;}
  };
  dynap,fastune,turns=1024;
  endtrack;
  
  system, "rm -rf temp";
  system, "mkdir temp";
  write,table=dynap,file="temp/dynap";
  write,table=dynaptune,file="temp/dynaptune";
  system,"/afs/cern.ch/group/si/slap/bin/foot < temp/dynaptune > temp/footprint";
};


PLOTFOOTPRINT(qxmin,qxmax,qymin,qymax) : macro = {
  delete,table=foottable;
  create,table=foottable,column=dQ1,dQ2;
  system,"/afs/cern.ch/eng/lhc/optics/HLLHCV1.0/beambeam/fillfoottable";
  call,file="temp/fillfoottable.madx";
  plot,style=100,noversion=true,nolegend=true,title=Footprint,table=foottable,
       colour=2,haxis=dq1,vaxis=dq2,hmin=qxmin,hmax=qxmax,vmin=qymin,vmax=qymax;
  system, "ps2pdf madx.ps && rm madx.ps && mv madx.pdf footprint.pdf";
};

exec MAKEFOOTPRINT();
exec PLOTFOOTPRINT(0.30,0.315,0.31,0.325);

quit;
